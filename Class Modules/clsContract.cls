VERSION 1.0 CLASS
BEGIN
  MultiUse = -1  'True
END
Attribute VB_Name = "clsContract"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = False
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = False
Option Explicit

'This Class is based on the current architecture for contracts
'which is generated by CongaMerge and uses fields to "turn on"
'and "turn off" riders.  ALL of the riders exist in a contract
'but ONLY the appropriate fields containing riders are "on"

Private mobjCBUtilities As Template
Private Const mstrADD_IN_NAME As String = "CB Utilities v3.dotm" 'Remember to look in Add-Ins List and get rid of this!!!
Private Const mstrAUTO_TEXT_HYPERLINK_CORRECTION As String = "HGH - HED Links - Guidelines"
Private Const mstrAUTO_TEXT_AMENDMENT As String = "HSA - HED Standard Addendum"

Private Sub Class_Initialize()
    On Error GoTo ErrorHandler
    'Rewrite this to look in the list of Add-Ins instead of hardcoding it with the constant!  I mean what happens when I go to version 4!!
'    Set mobjCBUtilities = Templates(Application.StartupPath & "\" & mstrADD_IN_NAME)



ExitHere:
    Exit Sub
    
ErrorHandler:
    With Err
        .Source = "Class_Initialize"
        .Raise .Number, .Source, .Description
    End With
End Sub

Private Sub Class_Terminate()
    On Error GoTo ErrorHandler
    
    Set mobjCBUtilities = Nothing

ExitHere:
    Exit Sub
    
ErrorHandler:
    With Err
        .Source = "Class_Terminate"
        .Raise .Number, .Source, .Description
    End With
End Sub

Public Sub Clean_Up_Riders()
    '************************************************************************************************
    'Author: Adrian Jones
    'Date Created: 08/24/2015
    'This routine exists in the class as well as here. The purpose for it HERE is to tie a keystroke
    'to this routine.
    'Alt C, R for C = Clean Up, R = Rider
    '************************************************************************************************
    
    Dim varRiders As Variant
    Dim fldRider As Field
    Dim idx As Integer
    
    On Error GoTo ErrorHandler
    
    Selection.HomeKey Unit:=wdStory
    varRiders = FindRidersUsingFieldObject()
    For idx = 0 To UBound(varRiders)
        Set fldRider = varRiders(idx)
        fldRider.Unlink
    Next idx
    
    For Each fldRider In ActiveDocument.Fields
        If fldRider.Type = wdFieldIf Then
            fldRider.Select
            Selection.Paragraphs(1).Range.Delete
        End If
    Next fldRider

ExitHere:
    Exit Sub
    
ErrorHandler:
    With Err
        .Source = "Clean_Up_Riders"
        .Raise .Number, .Source, .Description
    End With
End Sub

Public Sub Clean_Up_Riders_OLD()
    Dim fld As Field
    Dim rngRiderTitleParagraph As Range
    Dim rngRiderStart As Range
    Dim strRiderTitle As String
    Dim intRiderFieldCount As Integer
    Dim para As Paragraph
    Dim paras As Paragraphs
    
    On Error GoTo ErrorHandler
    For Each fld In ActiveDocument.Fields
        'Only look at "Rider" Fields
        If fld.Type = wdFieldIf Then
            intRiderFieldCount = intRiderFieldCount + 1
            Set rngRiderTitleParagraph = fld.Result.Paragraphs(1).Range
            Set rngRiderStart = rngRiderTitleParagraph.Next(Unit:=wdParagraph, Count:=1)
            strRiderTitle = rngRiderTitleParagraph.Text
            
'            Debug.Print intRiderFieldCount & ": " & strRiderTitle
'            fld.Select
'            Selection.Fields(1).Update
            If fld.Result.Paragraphs.Count = 1 Then
'                Debug.Print "Rider Title: " & strRiderTitle & "Paragraphs Count: " & fld.Result.Paragraphs.Count
            Else
                Debug.Print "Active Rider: " & strRiderTitle & "With a Paragraph Count of: " & fld.Result.Paragraphs.Count
                'Full justify all paragraphs
                Set paras = fld.Result.Paragraphs
                fld.Unlink
                For Each para In paras
                    para.Range.Select
                    If para.Alignment = wdAlignParagraphLeft Then
                        para.Alignment = wdAlignParagraphJustify
                    End If
                Next
                Debug.Print "UNLINKED FIELD/RIDER " & strRiderTitle
                'Place a page break before the rider
                rngRiderStart.ParagraphFormat.PageBreakBefore = True
            End If
            Debug.Print
            rngRiderTitleParagraph.Delete
            Debug.Print "DELETED FIELD/RIDER " & strRiderTitle
        End If
    Next fld
    'Debug.Print "Count of 'Rider Fields' " & intRiderFieldCount
    
ExitHere:
    Set rngRiderTitleParagraph = Nothing
    Set rngRiderStart = Nothing
    Exit Sub
    
ErrorHandler:
    With Err
        .Source = "Clean_Up_Riders_OLD"
        .Raise .Number, .Source, .Description
    End With
End Sub

Public Sub CleanUp_Fields()
    'This cleans up the hyperlinks: the "HED(1).docx" that doesn't belong
    'and corrects the "http://www.collegeboard.com/research/home"
    Dim fld As Field
    
    On Error GoTo ErrorHandler
    
    For Each fld In ActiveDocument.Fields
        If fld.Result = "HED (1).docx" Then
            fld.Result.Select
            Selection.Delete
            Selection.TypeText ("  ")
        End If
    Next fld
    
ExitHere:
    Exit Sub
    
ErrorHandler:
    With Err
        .Source = "CleanUp_Fields"
        .Raise .Number, .Source, .Description
    End With
End Sub

Public Sub CleanUp_General()
    'This cleans up the hyperlinks: the "HED(1).docx" that doesn't belong
    'and corrects the "http://www.collegeboard.com/research/home"
    Dim fld As Field
    Const strERRANT_TEXT As String = "HED (1).docx"
    Const strERRANT_TEXT_1 As String = "Error! Hyperlink reference not valid"
    Const strHYPERLINK_INCORRECT As String = "http://www.collegeboard.com/research/home"
    Const strERRANT_TEXT_MISSPELLED As String = "Servcice"
    
    Dim blnFound As Boolean
    
    On Error GoTo ErrorHandler
    
    '1. Clean up "HED (1).docx." This will be taken care of in the new riders
    Selection.HomeKey Unit:=wdStory
    Do
        blnFound = Selection.Find.Execute(FindText:=strERRANT_TEXT, ReplaceWith:=Space(2), Forward:=True, MatchCase:=False, MatchWholeWord:=False, MatchWildcards:=False)
        Selection.Collapse Direction:=wdCollapseEnd
    Loop Until Not blnFound
    
    Selection.HomeKey Unit:=wdStory
    Do
        blnFound = Selection.Find.Execute(FindText:=strERRANT_TEXT_1, ReplaceWith:=Space(2), Forward:=True, MatchCase:=False, MatchWholeWord:=False, MatchWildcards:=False)
        Selection.Collapse Direction:=wdCollapseEnd
    Loop Until Not blnFound
    
    '2. Swap out the incorrect hypertext
    Selection.HomeKey Unit:=wdStory
    Do
        blnFound = Selection.Find.Execute(FindText:=strHYPERLINK_INCORRECT, Forward:=True, MatchCase:=False, MatchWholeWord:=False, MatchWildcards:=False)
        If blnFound Then
            Templates(ThisDocument.FullName).BuildingBlockEntries(mstrAUTO_TEXT_HYPERLINK_CORRECTION).Insert Where:=Selection.Range, RichText:=True
        End If
        Selection.Collapse Direction:=wdCollapseEnd
    Loop Until Not blnFound
    
    '3. Clean up "Service" misspelling. Of course there's a better way of doing this but...AND this will be taken care of in the new rider
    'I'm just doing this because I'm lazy and I can.
    Selection.HomeKey Unit:=wdStory
    Selection.Find.Execute FindText:=strERRANT_TEXT_MISSPELLED, ReplaceWith:="Service", Forward:=True, MatchCase:=False, MatchWholeWord:=False, MatchWildcards:=False
    Selection.Collapse Direction:=wdCollapseEnd
        
ExitHere:
    Exit Sub
    
ErrorHandler:
    With Err
        .Source = "CleanUp_General"
        .Raise .Number, .Source, .Description
    End With
End Sub

Public Sub FormatDates()
    Dim blnFoundDate As Boolean
    On Error GoTo ErrorHandler:
    
    Const strREGEX_DATE_FORMAT_TERM As String = "[0-9]{1,}/[0-9]{1,}/[0-9]{4}*AM"
    
    Selection.HomeKey Unit:=wdStory
    Do
        blnFoundDate = Selection.Find.Execute(FindText:=strREGEX_DATE_FORMAT_TERM, Forward:=True, MatchWildcards:=True)
        If blnFoundDate Then
            FormatDateSpellOutMonth
        End If
        Selection.Collapse Direction:=wdCollapseEnd
    Loop Until Not blnFoundDate
    
ExitHere:
    Exit Sub
    
ErrorHandler:
    With Err
        .Source = "FormatDates"
        .Raise .Number, .Source, .Description
    End With
End Sub

Public Sub Clean_Up_EnrollmentBudgetTable()
    Dim tbl As Table
    Dim rowSubTotal As Row
    Dim rowDiscount As Row
    Dim rowTotalCost As Row
    Dim blnFoundSearchItem As Boolean
    Dim intCurrCol As Integer
    
    Const strROW_TEXT_DISCOUNT As String = "Total Discount: "
    Const strPAGE_BREAK_EBS As String = "Enrollment Budget" 'EBS = Enrollment Budget Schedule
    
    On Error GoTo ErrorHandler:
    
    'Go to the Enrollment Budget Schedule table
    'Assuming here it is the last table in the contract
    'because SalesForce generated contracts put it on
    'the last page
    Selection.GoTo What:=wdGoToTable, Which:=wdGoToLast
    Set tbl = Selection.Tables(1)
    
    'Move up to the title: "Enrollment Budget"
    Selection.MoveUp Unit:=wdParagraph
    Selection.MoveUp Unit:=wdParagraph
    Selection.ParagraphFormat.PageBreakBefore = True
   
    'If the table is uniform, it's not the Enrollment Budget Schedule
    If tbl.Uniform = False Then
        Set rowTotalCost = tbl.Rows.Last
        Set rowDiscount = rowTotalCost.Previous
        Set rowSubTotal = rowDiscount.Previous
        
        'Split the table so it can be sorted properly
        tbl.Split BeforeRow:=rowSubTotal
        tbl.Sort ExcludeHeader:=True, SortFieldType:=wdSortFieldDate, SortOrder:=wdSortOrderAscending, FieldNumber:=2
        
        'Fix the spaces in the Term dates
        tbl.Cell(1, 1).Select
        Selection.Collapse Direction:=wdCollapseStart
        Do
            blnFoundSearchItem = Selection.Find.Execute(Forward:=True, FindText:=" /", ReplaceWith:="/", MatchCase:=False, MatchWholeWord:=False, MatchWildcards:=False)
            Selection.Collapse Direction:=wdCollapseEnd
            Debug.Print "Fixed Term Dates" & blnFoundSearchItem
        Loop Until Not blnFoundSearchItem
        
        'Center quantities
        tbl.Cell(1, 1).Select
        Selection.Collapse Direction:=wdCollapseStart
        blnFoundSearchItem = Selection.Find.Execute(Forward:=True, FindText:="Quantity", MatchCase:=False, MatchWholeWord:=False, MatchWildcards:=False)
        If blnFoundSearchItem Then
            Selection.Columns(1).Select
            Selection.ParagraphFormat.Alignment = wdAlignParagraphCenter
            Debug.Print "Center Quantity" & blnFoundSearchItem
        End If
        
        'Fix the trademarks and registered trademarks
        tbl.Cell(1, 1).Select
        Selection.Collapse Direction:=wdCollapseStart
        Do
            blnFoundSearchItem = Selection.Find.Execute(Forward:=True, FindText:=" (TM)", ReplaceWith:="™", MatchCase:=False, MatchWholeWord:=False, MatchWildcards:=False)
            Selection.Collapse Direction:=wdCollapseEnd
            Debug.Print "Search: (TM) " & blnFoundSearchItem
        Loop Until Not blnFoundSearchItem
        
        tbl.Cell(1, 1).Select
        Selection.Collapse Direction:=wdCollapseStart
        Do
            blnFoundSearchItem = Selection.Find.Execute(Forward:=True, FindText:="(R)", ReplaceWith:="®", MatchCase:=False, MatchWholeWord:=False, MatchWildcards:=False)
            Selection.Collapse Direction:=wdCollapseEnd
            Debug.Print "Search: (R) " & blnFoundSearchItem
        Loop Until Not blnFoundSearchItem
        
        'Delete Discount column and Discount Row if discount is 0
        tbl.Cell(1, 1).Select
        Selection.Collapse Direction:=wdCollapseStart
        If Selection.Find.Execute(FindText:="Total Discount:", MatchCase:=False, MatchWholeWord:=False, MatchWildcards:=False) Then
            Selection.Rows(1).Select
            If Val(Mid(Selection.Text, Len("Total Discount: $ "))) = 0 Then
                tbl.Rows(1).Select
                If Selection.Find.Execute(FindText:="discount", MatchCase:=False, MatchWholeWord:=False, MatchWildcards:=False) Then
                    Selection.Columns(1).Delete
                    rowDiscount.Delete
                    rowSubTotal.Delete
                End If
            End If
        End If
        
        tbl.AutoFitBehavior wdAutoFitWindow
        tbl.AllowAutoFit = True
        
        tbl.Rows.Last.Select
        Selection.Collapse Direction:=wdCollapseEnd
        Selection.Delete
    End If

ExitHere:
    Set tbl = Nothing
    Set rowTotalCost = Nothing
    Set rowDiscount = Nothing
    Set rowSubTotal = Nothing
    Exit Sub
    
ErrorHandler:
    With Err
        .Source = "Clean_Up_EnrollmentBudgetTable"
        .Raise .Number, .Source, .Description
    End With
End Sub

Public Function FindRidersUsingFieldObject() As Variant
    Dim fld As Field
    Dim intActiveRiderFieldCount As Integer
    Dim objRiders() As Field
    Dim para As Paragraph
    Dim paras As Paragraphs
    On Error GoTo ErrorHandler
    
    intActiveRiderFieldCount = 0
    FindRidersUsingFieldObject = Array()
    
    For Each fld In ActiveDocument.Fields
        If fld.Type = wdFieldIf Then
            If fld.Result.Paragraphs.Count > 1 Then
                Set paras = fld.Result.Paragraphs
                ReDim Preserve objRiders(intActiveRiderFieldCount)
                Set objRiders(intActiveRiderFieldCount) = fld
                Debug.Print "Number of Paragraphs in Code: " & fld.Code.Paragraphs.Count
                Debug.Print "Number of Paragraphs in Result: " & fld.Result.Paragraphs.Count
                
                FindRidersUsingFieldObject = objRiders()
                
                'This really is for
                intActiveRiderFieldCount = intActiveRiderFieldCount + 1
                If fld.Update Then
                    Debug.Print "Field Updated Correctly"
                Else
                    Debug.Print "Field " & fld.Update & " has an error"
                End If
                
                Debug.Print
                
                For Each para In paras
                    para.Range.Select
                    If para.Alignment = wdAlignParagraphLeft Then
                        para.Alignment = wdAlignParagraphJustify
                    End If
                Next
            End If
        End If
    Next fld
    Debug.Print
    Debug.Print "Number of Fields in this Document that are Riders: " & intActiveRiderFieldCount

Exit_Here:
    Set paras = Nothing
    Exit Function
    
ErrorHandler:
    With Err
        MsgBox .Number & vbCr & .Description, vbCritical, "FindRidersUsingFieldObject"
    End With
    Resume Exit_Here
End Function

Public Sub CreateAmendmentFromMain()
    Dim lngResult As Long
    Dim lngPageNumberSignaturePage As Long
    Dim blnFound As Boolean
    
    Const strMAIN_AGREEMENT_NAME As String = "Enrollment"
    Const strAUTOTEXT_AMENDMENT As String = "HSA - HED Standard Addendum"
    Const intSIGNATURE_PAGE_AMENDMENT As Integer = 2

    On Error GoTo ErrorHandler
    
    'Added on 02/08/2018 to bail when the agreement is NOT a HED agreement
    blnFound = Selection.Find.Execute(FindText:=strMAIN_AGREEMENT_NAME)
    
    If blnFound Then
        lngResult = MsgBox("This deletes pages in the main part of the agreement " & vbCr _
            & "up to the signature page and replaces those removed pages " & vbCr _
            & "with the standard Higher Education Amendment Page", vbYesNoCancel + vbExclamation)
        
        If lngResult = vbYes Then
            lngPageNumberSignaturePage = FindSignaturePage
        
            Selection.HomeKey Unit:=wdStory
            Selection.Extend
            Selection.GoTo What:=wdGoToPage, Which:=wdGoToNext, Name:=lngPageNumberSignaturePage
            Selection.Delete
            Application.Templates(ThisDocument.FullName).BuildingBlockEntries(strAUTOTEXT_AMENDMENT).Insert Where:=Selection.Range, RichText:=True
            
            'Remove paragraph page before and consolidate signature page and Amendment page.
            Selection.GoTo What:=wdGoToPage, Which:=wdGoToNext, Name:=intSIGNATURE_PAGE_AMENDMENT
            Selection.Range.ParagraphFormat.PageBreakBefore = False
            Selection.HomeKey Unit:=wdStory
        End If
    Else
        'Added on 02/08/2018 to bail when the agreement is NOT a HED agreement
        MsgBox "Document does not appear to be a Higher Ed Agreement" & vbCr _
            & "Open a Higher Ed Agreement then run again", vbExclamation + vbOKOnly
    End If
    
    
Exit_Here:
    Exit Sub

ErrorHandler:
    With Err
        MsgBox .Number & vbCr & .Description, vbCritical, "CreateAmendmentFromMain"
    End With
    Resume Exit_Here
End Sub

Private Function FindSignaturePage() As Long
    Dim rngSignaturePage As Range
    Dim tmpRange As Range
    
    Const strFIND_SIGNATURE_PAGE_TEXT As String = "Signature"
    
    On Error GoTo ErrorHandler
    
    FindSignaturePage = 1
    
    Set tmpRange = ActiveDocument.StoryRanges(wdMainTextStory)
    Do
        tmpRange.Find.Execute FindText:=strFIND_SIGNATURE_PAGE_TEXT, Wrap:=wdFindContinue
        tmpRange.Select
    Loop Until (tmpRange.Find.Found = True And tmpRange.Information(wdWithInTable))
    
    Debug.Print Selection.Information(wdActiveEndPageNumber)
    
    FindSignaturePage = Selection.Information(wdActiveEndPageNumber)
    
Exit_Here:
    Set tmpRange = Nothing
    Exit Function

ErrorHandler:
    With Err
        MsgBox .Number & vbCr & .Description, vbCritical, "FindSignaturePage"
    End With
    Resume Exit_Here
End Function

Public Sub CreateK12AmendmentfromTemplate(ByRef rstrTemplateFullName As String)
    Dim K12AmendmentTemplate As String
    On Error GoTo ErrorHandler
    
    K12AmendmentTemplate = "C:\Documents and Settings\ajones\Application Data\Microsoft\Templates\Contracts Management\Automated Templates\K12 Amendment Template v3 Final.dotx"
    K12AmendmentTemplate = rstrTemplateFullName
    Documents.Add Template:=K12AmendmentTemplate
    If Not ActiveDocument.TrackRevisions Then
        ActiveDocument.TrackRevisions = True
    End If
     
Exit_Here:
    Exit Sub

ErrorHandler:
    With Err
        MsgBox .Number & vbCr & .Description, vbCritical, "CreateK12AmendmentfromTemplate"
    End With
    Resume Exit_Here
End Sub

Public Sub CreateK12OrHEDAmendment()
    On Error GoTo ErrorHandler
    
    'REMEMBER TO REDO THIS BY TAKING THE FORM OUT OF THIS CLASS (Assbackwards)
    'Determine if K12 or HED contract that is currently active
'    frmAmendment.Show
    
    'If HED run CreateAmendmentFromMain like I have been
'    If frmAmendment.Tag = "K12" Then
'        CreateK12AmendmentfromTemplate
'    ElseIf frmAmendment.Tag = "HED" Then
'        CreateAmendmentFromMain
'    End If
    
    'else
    
Exit_Here:
    Unload frmAmendment
    Exit Sub

ErrorHandler:
    With Err
        MsgBox .Number & vbCr & .Description, vbCritical, "CreateK12OrHEDAmendment"
    End With
    Resume Exit_Here
End Sub
